apply plugin: 'java'
apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
    // gradle versions
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.1'
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.8.0' // CURRENT_GRPC_VERSION

dependencies {
  compile "com.google.api.grpc:proto-google-common-protos:0.1.9"
  compile "io.grpc:grpc-netty:${grpcVersion}"
  compile "io.grpc:grpc-protobuf:${grpcVersion}"
  compile "io.grpc:grpc-stub:${grpcVersion}"
  compile 'io.netty:netty-tcnative-boringssl-static:2.0.6.Final'
  compile 'commons-cli:commons-cli:1.4'
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.4.0'
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

// Generate IntelliJ IDEA's .idea & .iml project files
apply plugin: 'idea'
apply plugin: 'eclipse'

// Provide convenience executables for trying out the examples.
apply plugin: 'application'

startScripts.enabled = false

task gnmiClient(type: CreateStartScripts) {
	mainClassName = 'gnmi.GnmiClient'
	applicationName = 'gnmi-client'
	outputDir = new File(project.buildDir, 'tmp')
	classpath = jar.outputs.files + project.configurations.runtime
}

task gnmiServer(type: CreateStartScripts) {
	mainClassName = 'gnmi.GnmiServer'
	applicationName = 'gnmi-server'
	outputDir = new File(project.buildDir, 'tmp')
	classpath = jar.outputs.files + project.configurations.runtime
}


applicationDistribution.into('bin') {
  from(gnmiClient)
  from(gnmiServer)
  fileMode = 0755
}
